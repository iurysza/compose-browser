configurations { detekt }
dependencies { detekt "io.gitlab.arturbosch.detekt:detekt-cli:$detektVersion" }

task detektCi(type: JavaExec, group: "verification") {
    description = "Run Kotlin static analysis on changed files."
    main = "io.gitlab.arturbosch.detekt.cli.Main"
    classpath = configurations.detekt
    def changedFilesList = getDiffedFilesFromBranch("main")
    if (changedFilesList.isEmpty()) {
        changedFilesList = "quality/"
    }
    doFirst {
        if (changedFilesList.isEmpty()) {
            println("\nNo kotlin files changed! Skipping detektCi task")
        } else {
            println("\nRunning detekt on the changed files:")
            changedFilesList
                    .split(",")
                    .each { println("$projectDir/$it") }
        }
    }

    def reportDir = "${rootProject.buildDir}/reports/detekt"
    def params = [
            "--input", "${changedFilesList}",
            "--config", "${rootProject.rootDir}/quality/detekt-config.yml",
            "--report", "xml:$reportDir/detekt-checkstyle.xml",
            "--report", "html:$reportDir/report.html"
    ]
    args(params)
}

private static String getDiffedFilesFromBranch(String branch) {
    def input = new ByteArrayOutputStream()
    def cmd = "git diff --diff-filter=d --name-only origin/$branch --relative | grep '\\.kt\\?\$'"
    execute(cmd, input)

    return input.toString()
            .trim()
            .replace("\n", ",")
}

private static def execute(cmd, output) {
    ['sh', '-c', cmd].execute().waitForProcessOutput(output, System.err)
}
